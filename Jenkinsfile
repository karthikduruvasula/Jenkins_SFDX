pipeline {
  agent any
  // --- Select Environment to Deploy ---
  parameters {
      choice(
          name: 'TARGET_ENV', 
          choices: ['dev', 'qa', 'uat', 'prod'], 
          description: 'Select the Environment to Authenticate and Deploy'
      )
  }
  environment {
      // --- CREDENTIALS ---
      PROD_CLIENT_ID = credentials('ad051466-3a4f-43c7-8553-13757ba739c8')  // Consumer Key from your Connected App in Production
      JWT_KEY_FILE   = credentials('7e390c5b-1f9b-4c03-8355-a04e93aed514') // server.key file for JWT Authentication
      SF_ALIAS = "${params.TARGET_ENV}_org" // Default alias based on selected environment
  }
  stages { 
    // --- STAGE 1: Checkout SCM ---
    stage('Checkout SCM') { 
      steps {
        script {
          checkout scm // Checkout the code from the configured SCM
          sh "git fetch origin main" // Fetch latest main branch for diff comparison
        }
      }
    }// End of Stage 1  
    // --- STAGE 2: Initialize & Authenticate ---
    stage('Initialize & Authenticate') { 
      steps {
        script {
          // --- Initialize ---
          def sf_instance_url = "https://test.salesforce.com"
          def sf_username = ""
          if (params.TARGET_ENV == 'dev') {
              sf_username = "kduruvasula-demo@salesforce.com.dev1"
          } 
          else if (params.TARGET_ENV == 'qa') {
              sf_username = "kduruvasula-demo@salesforce.com.qa"
          } 
          else if (params.TARGET_ENV == 'uat') {
              sf_username = "kduruvasula-demo@salesforce.com.uat"
          } 
          else if (params.TARGET_ENV == 'prod') {
              sf_username = "kduruvasula-demo@salesforce.com"
              sf_instance_url = "https://login.salesforce.com"
              env.SF_ALIAS = "prod_org"
          }
          echo "------------------------------------------------"
          echo "Target Org Alias:    ${env.SF_ALIAS}"
          echo "Instance URL:        ${sf_instance_url}"
          echo "Authenticating User: ${sf_username}"
          echo "------------------------------------------------"
          // --- Authenticate ---
          sh """
              sf org login jwt \
                  --client-id "${env.PROD_CLIENT_ID}" \
                  --jwt-key-file "${env.JWT_KEY_FILE}" \
                  --username "${sf_username}" \
                  --instance-url "${sf_instance_url}" \
                  --alias "${env.SF_ALIAS}" \
                  --set-default
          """
        }
      }
    } // End of Stage 2
    // --- STAGE 3: Generate Delta (Diff) ---
    stage('Generate Delta') {
      steps {
        script {
          echo "Calculating differences between HEAD and origin/main..."
          // 1. Create output directory
          sh "mkdir -p changed-sources"
          // 2. Run SFDX Git Delta
          // --to "HEAD": The current commit
          // --from "origin/main": The base branch
          // --output: Where to save the package.xml
          sh """
              sf sgd:source:delta \
                  --to "HEAD" \
                  --from "origin/main" \
                  --output-dir "./changed-sources" \
                  --generate-delta
          """
          // 3. Debug: Show the generated package.xml
          sh "cat changed-sources/package/package.xml"
        }
      }
    } // End of Stage 3
    // --- STAGE 4: Validate Changes (Check-Only) ---
    stage('Validate Changes') {
      steps {
        script {
          // Check if package.xml exists (it might not if there are no metadata changes)
          if (fileExists('changed-sources/package/package.xml')) {
              echo "Validating delta changes against ${env.SF_ALIAS}..."
              
              // --dry-run: Validates without saving
              // --manifest: Uses the package.xml generated by the delta step
              sh """
                  sf project deploy start \
                      --dry-run \
                      --manifest changed-sources/package/package.xml \
                      --target-org "${env.SF_ALIAS}" \
                      --wait 10
              """
          } else {
              echo "No metadata changes found. Skipping validation."
          }
        }
      }
    } // End of Stage 4

  // --- STAGE 5: Deploy Changes (Actual) ---
  stage('Deploy Changes') {
    steps {
      script {
        if (fileExists('changed-sources/package/package.xml')) {
            echo "Deploying delta changes to ${env.SF_ALIAS}..."
            
            // Removes --dry-run to perform actual deployment
            sh """
                sf project deploy start \
                    --manifest changed-sources/package/package.xml \
                    --target-org "${env.SF_ALIAS}" \
                    --wait 10
            """
        } else {
            echo "No metadata changes found. Skipping deployment."
        }
      }
    }   
  }   // End of Stage 5 
 }   // End of Stages
} // End of Pipeline
